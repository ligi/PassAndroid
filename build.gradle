buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.1'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath('com.stanfy.spoon:spoon-gradle-plugin:0.14.1') {
            // https://github.com/stanfy/spoon-gradle-plugin/issues/33
            exclude module: 'guava'
        }

    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'

repositories {
    jcenter()
}

android {

    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        versionCode 244
        versionName "2.4.4"
        minSdkVersion 9
        targetSdkVersion 21
        applicationId "org.ligi.passandroid"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "maps", "analytics", "distribution"

    productFlavors {

        withMaps {
            flavorDimension "maps"
        }

        noMaps {
            flavorDimension "maps"
        }

        withAnalytics {
            flavorDimension "analytics"
        }

        noAnalytics {
            flavorDimension "analytics"
        }

        forFDroid {
            flavorDimension "distribution"
        }

        forPlay {
            flavorDimension "distribution"
        }
    }

    // needed for assertJ
    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // hack for instrumentation testing :-(
        exclude 'LICENSE.txt'
    }

    // https://groups.google.com/forum/#!topic/adt-dev/-Km7VgExKf4
//    useOldManifestMerger true
}

dependencies {
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'

    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.2'

    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'

    // have to wait with update till this is solved: https://code.google.com/p/dexmaker/issues/detail?id=40
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    // The Apache Software License, Version 2.0
    compile 'com.googlecode.android-query:android-query:0.25.9'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'net.lingala.zip4j:zip4j:1.3.2'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'joda-time:joda-time:2.7'
    compile 'com.squareup:otto:1.3.6'

    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.0'

    compile 'com.melnykov:floatingactionbutton:1.1.0'
    compile 'com.afollestad:material-dialogs:0.6.2.3'

    compile 'org.ligi:AXT:0.31'
    compile 'org.ligi:tracedroid:1.4'
    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'fr.nicolaspomepuy:discreetapprate:2.0.3@aar'
    compile 'com.larswerkman:HoloColorPicker:1.4'

    //compile 'fr.baloomba:viewpagerindicator:2.4.2'
    compile('com.mcxiaoke.viewpagerindicator:library:2.4.1') {
        exclude group: 'com.android.support'
    }
    // cannot use this new version: https://github.com/zxing/zxing/issues/165
    // WARNING: might work on some devices or the emulator - but fails on others
    // compile 'com.google.zxing:core:3.1.0'

    compile fileTree(dir: 'libs', include: 'zxing-core-2.3.0-SNAPSHOT.jar')

    def play_services = 'com.google.android.gms:play-services-maps:6.5.+'

    withAnalyticsCompile play_services
    withMapsCompile play_services
}
